/* Copyright 2018 John S. Burwell III
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
      mavenCentral()
      maven { url "https://repo.spring.io/libs-snapshot" }
    }
    dependencies {
        classpath "org.junit.platform:junit-platform-gradle-plugin:1.0.2"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:2.0.0.BUILD-SNAPSHOT"
    }
}

plugins {
  id "java"
  id "idea"
  id "com.palantir.docker" version "0.17.2"
  id "net.ltgt.errorprone" version "0.0.13"
  id "net.minecrell.licenser" version "0.3"
}

apply plugin: "org.junit.platform.gradle.plugin"
apply plugin: "org.springframework.boot"

bootJar {
  baseName = "jv-server"
  version = "0.1.0-SNAPSHOT"
}

repositories {
  mavenCentral()
  jcenter()
  maven { url "https://jitpack.io" }
  maven { url "https://repo.spring.io/libs-snapshot/" }
}

ext {
  javaVersion =  "1.8"
  versions = [
    assertj: [
        core: "3.8.0",
        guava: "3.1.0",
    ],
    errorprone: "2.1.3",
    gradle: "4.4",
    guava: "23.6-jre",
    jqwik: "0.7.3",
    junit: [
        platform: "1.0.2",
        jupiter: "5.0.2",
    ],
    spring: [
//        boot: "1.5.9.RELEASE",
        boot: "2.0.0.BUILD-SNAPSHOT",
        core: "5.0.3.RELEASE",
        test_junit5: "1.0.3"
    ]
  ]
}

group = "net.cockamamy.jv"

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

license {
    header = project.file('HEADER.txt')
    include "**/*.java"
}

junitPlatform {
    filters {
        includeClassNamePattern '.*Test'
        includeClassNamePattern '.*Tests'
        includeClassNamePattern '.*Properties'
    }
    enableStandardTestTask true
}

dependencies {
    compile "com.google.guava:guava:${versions.guava}"
    compile ("org.springframework.boot:spring-boot-starter-web:${versions.spring.boot}") {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-json"
        exclude group: "org.springframework.boot", module: "spring-boot-starter-tomcat"
        exclude group: "org.hibernate.validator", module: "hibernate-validator"
    }
    compile ("org.springframework.boot:spring-boot-starter-undertow:${versions.spring.boot}") {
        exclude group: "io.undertow", module: "undertow-websockets-jsr"
    }

    errorprone "com.google.guava:guava:${versions.guava}"
    errorprone "com.google.errorprone:error_prone_core:${versions.errorprone}"

    testCompile "org.junit.platform:junit-platform-launcher:${versions.junit.platform}"
    testCompile "com.github.jlink:jqwik:${versions.jqwik}"
    testCompile "org.junit.jupiter:junit-jupiter-engine:${versions.junit.jupiter}"
    testCompile "org.assertj:assertj-core:${versions.assertj.core}"
    testCompile "org.assertj:assertj-guava:${versions.assertj.guava}"
    testCompile ("com.google.guava:guava-testlib:${versions.guava}") {
        exclude group: "junit", module: "junit"
    }
    testCompile ("org.springframework.boot:spring-boot-starter-test:${versions.spring.boot}") {
        exclude group: "junit", module: "junit"
    }
}

docker {
    dependsOn assemble
    name "${group}/${bootJar.baseName}"
    dockerfile file("Dockerfile")
    files bootJar.archivePath
    buildArgs([JAR_FILE: "${bootJar.archiveName}"])
}

